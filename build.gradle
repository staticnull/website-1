buildscript {
    repositories {
        mavenLocal()
        maven { url "https://repo.grails.org/grails/core" }
    }
    dependencies {
        classpath "org.grails:grails-gradle-plugin:$grailsVersion"
        classpath "com.bertramlabs.plugins:asset-pipeline-gradle:$assetPipelineVersion"
        classpath "org.grails.plugins:hibernate5:6.0.4"
        classpath 'org.ajoberstar:grgit:1.7.2'
    }
}

plugins {
    id 'com.craigburke.client-dependencies' version '1.3.1'
    id "fi.evident.beanstalk" version "0.0.7"
}

ext {
    git = org.ajoberstar.grgit.Grgit.open(file('.'))
    revision = git.head().abbreviatedId
}
version revision
group "gr8conf"

apply plugin: "org.springframework.boot"
apply plugin: "idea"
apply plugin: "org.grails.grails-web"
apply plugin: "org.grails.grails-gsp"
apply plugin: "asset-pipeline"

repositories {
    mavenLocal()
    maven { url "https://repo.grails.org/grails/core" }
}

dependencyManagement {
    imports {
        mavenBom "org.grails:grails-bom:$grailsVersion"
    }
    applyMavenExclusions false
}

dependencies {
    compile "org.springframework.boot:spring-boot-starter-logging"
    compile "org.springframework.boot:spring-boot-autoconfigure"
    compile "org.grails:grails-core"
    compile "org.springframework.boot:spring-boot-starter-actuator"
    compile "org.springframework.boot:spring-boot-starter-tomcat"
    compile "org.grails:grails-dependencies"
    compile "org.grails:grails-web-boot"
    compile "org.grails.plugins:cache"
    compile "org.grails.plugins:scaffolding"
    compile "org.grails.plugins:hibernate5"
    compile "org.hibernate:hibernate-core:5.1.2.Final"
    compile "org.hibernate:hibernate-ehcache:5.1.2.Final"
    console "org.grails:grails-console"
    compile "org.grails:grails-datastore-rest-client"
    compile "org.hibernate:hibernate-ehcache"
    console "org.grails:grails-console"
    profile "org.grails.profiles:web"
    runtime "com.bertramlabs.plugins:asset-pipeline-grails:$assetPipelineVersion"
    runtime "com.h2database:h2"
    testCompile "org.grails:grails-plugin-testing"
    testCompile "org.grails.plugins:geb"
    testRuntime "org.seleniumhq.selenium:selenium-htmlunit-driver:2.47.1"
    testRuntime "net.sourceforge.htmlunit:htmlunit:2.18"
}


clientDependencies {
    bower {
        'fontawesome'('4.7.0') {
            include 'css/**', 'fonts/**'
        }
        'bootstrap'('v3.3.7')
        'bootstrap-material-design'('0.5.10', exclude: 'bootstrap-sass')
        'jquery'('3.1.1')
        'jquery.countdown'('2.2.0')
        'fullcalendar'('3.1.0') {
            exclude 'locale/**'
        }
        'fullcalendar-scheduler'('1.5.0')
        'moment'('2.17.1') {
            include 'moment.js'
        }
    }
    yarn {
        'turbolinks'('5.0.0')
    }
}

clean << {
    delete "${rootDir}/grails-app/assets/vendor/"
}

task wrapper(type: Wrapper) {
    gradleVersion = gradleWrapperVersion
}

bootRun {
    jvmArgs('-Dspring.output.ansi.enabled=always')
}

assets {
    minifyJs = true
    minifyCss = true
}

jar {
    archiveName 'gr8confus-website.jar'

    from('src/main/ebextensions') {
        into('.ebextensions')
    }
}

beanstalk {
    profile = 'gr8conf'
    s3Endpoint = "s3-us-east-2.amazonaws.com"
    beanstalkEndpoint = "elasticbeanstalk.us-east-2.amazonaws.com"

    deployments {
        dev {
            war = "build/libs/gr8confus-website.jar"
            application = 'gr8conf-website'
            environment = 'gr8conf-website-dev'
        }
        prod {
            war = "build/libs/gr8confus-website.jar"
            application = 'gr8conf-website'
            environment = "gr8conf-website-prod"
        }
    }
}

deployDev.dependsOn "assemble"
deployProd.dependsOn "assemble"
